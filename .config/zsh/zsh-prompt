#!/bin/sh

# Autoload vcs and colors
autoload -Uz vcs_info
autoload -U colors && colors

# Setup a hook that runs before every prompt. 
precmd_vcs_info() { vcs_info }
precmd_functions+=( precmd_vcs_info )
setopt prompt_subst

# Add a function to check for untracked files in the directory.
zstyle ':vcs_info:git*+set-message:*' hooks git-untracked
+vi-git-untracked(){
    if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]] && \
        git status --porcelain | grep '??' &> /dev/null ; then
        # This will show the marker if there are any untracked files in repo.
        # If instead you want to show the marker only if there are untracked
        # files in $PWD, use:
        #[[ -n $(git ls-files --others --exclude-standard) ]] ; then
        hook_com[staged]+='!' # signify new files with a bang
    fi
}

# enable only git 
zstyle ':vcs_info:*' enable git 
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:git:*' formats "(%r)%b%u%c"

# Main prompt
PS1="%B%{$fg[blue]%}[%{$fg[magenta]%}%2~%{$fg[blue]%}]%{$fg[red]%}$%{$reset_color%}%b "
RPROMPT="\$vcs_info_msg_0_"
